{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "5c3b496f-3ab7-4710-b07a-69f9507ea136",
   "metadata": {},
   "source": [
    "## Computation of persistent homology with R or Z/pZ coeficients\n",
    "\n",
    "Suppose we are given a filtration of abstract simplicial complexes $K_1 \\le K_2 \\le \\cdots \\le K_n$, where each $K_i$ is given as a set of maximal simplices of $K_i$ that did no appear as simplices of the previous level of the filtration. Our task will be to determine the persistent homology groups of this filtration with coefficients in a field $F$, where $F$ is $\\mathbb{R}$ or $\\mathbb{Z}/p\\mathbb{Z}$ for a prime $p$.\n",
    "\n",
    "### Basic data manipulation\n",
    "\n",
    "Our filtration will be biven as a list `K = [K1, K2, ..., Kn]` where each `Ki` is given as a list of maximal simplices as described above, e.g. `K = [[(1,), (3,)], [(2,4)], [(1,4), (2,3)], [(3,4)], [(1,2)], [(1,2,4)]]`. We present each simplex as a tuple (e.g. $\\tau = (1,2,3)$). We will not build a full simplicial complex for each level of the filtration, but will only add new simplices as they appear at each level. Building on what we did earlier, we will present this as a list of 'dimension dictionaries', where each dictionary corresponds to a level of the filtration with only 'new' simplices. Each `key => value` pair is of the form $d$ `=>` list of 'new' simplices of dimension $d$ in $K_i$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d6105636-a1ab-4662-9179-bef83d0adb3c",
   "metadata": {},
   "outputs": [],
   "source": [
    "# To build the boundary matrix (or matrices) we will need the filtration of the full simplicial\n",
    "# complex Kn. We will represent each stage of the filtration with a 'dimension dictionary'  \n",
    "# that contains all simplices of the current level Ki, which are not contained \n",
    "# in the previous levels.\n",
    "\n",
    "# Compute the boundary of a simplex sx and represent it as a list of maximal simplices, \n",
    "# i.e., just determine the codimenstion 1 faces of sx. \n",
    "# E.g. for sx = (1, 2, 3) we get [(1, 2), (1, 3), (2, 3)].\n",
    "function simplex_boundary(sx)\n",
    "    # determine the codimension 1 simplices in the boundary of the simplex sx\n",
    "    # 'remove' one entry of a tuple at each specific index by splicing the corresponding subtuples\n",
    "    cd1faces = ([(sx[1:k-1]..., sx[k+1:end]...) for k in eachindex(sx)])\n",
    "    return cd1faces\n",
    "end\n",
    "\n",
    "# Create a vector of 'dimension dictionaries' representing each level of the filtration\n",
    "function dim_dict_filtration(max_filtration)\n",
    "    # determine the dimension+1 of each level of the filtration\n",
    "    N = [maximum([length(sx) for sx in Ki]) for Ki in max_filtration]\n",
    "    # set up the dictionaries (empty vectors of k-tuples in each dimension)\n",
    "    dict_filtration = [Dict((k-1) => Vector{NTuple{k, Int}}() for k in 1:n) for n in N]\n",
    "    \n",
    "    # fill in the dictionaries for each level i of the filtration\n",
    "    for i in eachindex(N)\n",
    "        # first, add all simplices in max_filtration[i] to dict_filtration[i] \n",
    "        # in appropriate dimensions\n",
    "        for sx in max_filtration[i]\n",
    "            # check if sx is already contained in previous levels of the filtration\n",
    "            # (For proper inputs, these checks should not be needed.)\n",
    "            is_contained = false\n",
    "            for j in 1:(i-1)\n",
    "                is_contained |= !isnothing(findfirst(==(sx), dict_filtration[j]))\n",
    "                if is_contained\n",
    "                    break\n",
    "                end\n",
    "            end\n",
    "            # add the simplex and sort its vertices (this simplifies recognition of simplices later)\n",
    "            if !is_contained\n",
    "                push!(dict_filtration[i][length(sx)-1], Tuple(sort(collect(sx))))\n",
    "            end\n",
    "        end\n",
    "        \n",
    "        # second, add all faces of the simplices in max_filtration[i] \n",
    "        # (apart from those that are already contained in previous levels)\n",
    "        for k in (N[i]-1):-1:1\n",
    "            for sx in dict_filtration[i][k]\n",
    "                for bsx in simplex_boundary(sx)\n",
    "                    # check if bsx is contained in previous levels\n",
    "                    is_contained = false\n",
    "                    for j in 1:(i-1)\n",
    "                        if length(bsx)-1 in keys(dict_filtration[j])\n",
    "                           is_contained |= !isnothing(findfirst(==(bsx), dict_filtration[j][length(bsx)-1]))\n",
    "                        end\n",
    "                        # stop the search if already found in one of the previous levels\n",
    "                        if is_contained\n",
    "                            break\n",
    "                        end\n",
    "                    end\n",
    "                    # if it is not contained (and not already in the current level), \n",
    "                    # add it to the current level\n",
    "                    if !is_contained && isnothing(findfirst(==(bsx), dict_filtration[i][k-1]))\n",
    "                        push!(dict_filtration[i][k-1], bsx)\n",
    "                    end\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return dict_filtration\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e07d2852",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Let's test this.\n",
    "K = [[(1,), (3,)], [(2,4)], [(1,4), (2,3)], [(3,4)], [(1,2)], [(1,2,4)]]\n",
    "Kdict = dim_dict_filtration(K)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a4440933",
   "metadata": {},
   "source": [
    "### Building the boundary matrix D\n",
    "\n",
    "We will build a 'big' boundary matrix $D$ for the computation of persitent homology. We order the simplices according to the level of the filtration, and by dimension in each level of the filtration. This order then represents row and column index in the matrix $D$.\n",
    "\n",
    "Since $D$ is a mostly zero matrix, we represent it as a sparse array. We need `using SparseArrays`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "012dbc3b-72d6-4bc1-b874-b6bb2f70c035",
   "metadata": {},
   "outputs": [],
   "source": [
    "# an auxiliary function to find the 'index' of a simplex in the 'dimension dictionary'\n",
    "# of a filtration\n",
    "function simplex_index(sx, dim_dict_filt)\n",
    "    # initialize index at 0\n",
    "    index = 0\n",
    "    # find sx in dim_dict_filt\n",
    "    for k in eachindex(dim_dict_filt)\n",
    "        if length(sx)-1 in keys(dim_dict_filt[k])\n",
    "            l = findfirst(==(sx), dim_dict_filt[k][length(sx)-1])\n",
    "            if !isnothing(l)\n",
    "                # return the index as soon as the simplex is found\n",
    "                return index + l + sum(Vector{Int}([length(dim_dict_filt[k][dim]) for dim in 0:(length(sx)-2)]))\n",
    "            end\n",
    "        end\n",
    "        index += sum([length(dim_dict_filt[k][dim]) for dim in keys(dim_dict_filt[k])])\n",
    "    end\n",
    "end\n",
    "\n",
    "# boundary matrix for persistent homology in one large matrix\n",
    "function persistent_boundary(dim_dict_filt, p = 0)\n",
    "    # check if p is 0 or prime\n",
    "    if p != 0 && !isprime(p)\n",
    "        throw(DomainError(p, \"p should be 0 or a prime.\"))\n",
    "    end\n",
    "    # determine the size of this huge matrix\n",
    "    N = sum([sum([length(cplx[key]) for key in keys(cplx)]) for cplx in dim_dict_filt])\n",
    "    # use a sparse array for this mostly 0 matrix\n",
    "    pbd_matrix = spzeros(p == 0 ? Float64 : Mod{p}, N, N)\n",
    "    # fill this matrix with +/-1 at appropriate positions\n",
    "    col_ind = 1\n",
    "    for k in eachindex(dim_dict_filt)\n",
    "        # Keys (which represent the dimension of a simplex in our case) aren't sorted by default.\n",
    "        # We need to sort them.\n",
    "        for dim in sort(collect(keys(dim_dict_filt[k])))\n",
    "            for j in eachindex(dim_dict_filt[k][dim])\n",
    "                # only simplices of dimension >=1 have nonempty boundary\n",
    "                if length(dim_dict_filt[k][dim][j]) >= 2\n",
    "                    # find the boundary of the current simplex\n",
    "                    bsx = simplex_boundary(dim_dict_filt[k][dim][j])\n",
    "                    for bsx_ind in eachindex(bsx)\n",
    "                        # and add the corresponding +/-1 entry into the matrix\n",
    "                        pbd_matrix[simplex_index(bsx[bsx_ind], dim_dict_filt), col_ind] = (-1)^(bsx_ind+j)\n",
    "                    end\n",
    "                end\n",
    "                col_ind += 1\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return pbd_matrix\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "88b072d9-a071-421c-bf1e-0c4ce8dab847",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Let's continue testing.\n",
    "using SparseArrays, Mods, Primes\n",
    "D = persistent_boundary(Kdict, 3)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c7810a39",
   "metadata": {},
   "source": [
    "## Persistent homology computation\n",
    "\n",
    "To compute persistent homology groups (or the corresponding Betti numbers) we need to column-reduce the boundary matrix $D$. To keep track of the generators, we perform the same operations on an additional identity matrix."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e54efe1f-5749-4924-97d3-3aec62d7b3d2",
   "metadata": {},
   "outputs": [],
   "source": [
    "# column reduction (for a persistence boundary matrix)\n",
    "# This function will reduce D in place and return a matrix G. The columns of G represent coefficients of a linear combination of corresponding simplices which represents \n",
    "# a generator of homology.\n",
    "function column_reduce(D)\n",
    "    # The matrix G will keep track of the generators. We start with an identity matrix of the same size \n",
    "    # (and same entry type) as D.\n",
    "    G = sparse(1:size(D, 2), 1:size(D, 2), [eltype(D)(1) for _ in 1:size(D, 2)])\n",
    "    # initiate the column index and iterate until all columns are exhausted\n",
    "    j = 1\n",
    "    while j <= size(D, 2)\n",
    "        # find lowest nonzero entry in the current column\n",
    "        k = findlast(!=(0), D[:, j])\n",
    "        if isnothing(k)\n",
    "            # go to the next column if no such entries\n",
    "            j += 1\n",
    "            continue\n",
    "        end\n",
    "        for jj in 1:(j-1)\n",
    "            # find lowest nonzero entry in previous columns\n",
    "            kk = findlast(!=(0), D[:, jj])\n",
    "            # if one is found with the same row index as the lowest entry in the current column ...\n",
    "            if !isnothing(kk) && kk == k\n",
    "                # perform column reduction ... \n",
    "                scale = D[k, j]/D[k, jj]\n",
    "                D[:, j] -= scale*D[:, jj]\n",
    "                # ... do the same operation on G ...\n",
    "                G[:, j] -= scale*G[:, jj]\n",
    "                # ... and repeat\n",
    "                break\n",
    "            end\n",
    "            # once all previous columns are exhausted ...\n",
    "            if jj == j-1\n",
    "                # ... go to the next column\n",
    "                j += 1\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    # 'un-store' the zero entries that appeared in the sparse matrix D\n",
    "    dropzeros!(D)\n",
    "    return G\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fdc46878",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Another test.\n",
    "column_reduce(D)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.2",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
